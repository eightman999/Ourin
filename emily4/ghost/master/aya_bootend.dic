///////// 起動/終了/切り替えトーク辞書

//**** 初回起動 *************************************************************************

//---- OnFirstBootイベント --------------------------------------------------------------
OnFirstBoot
{
	//---- 変数の初期化
	if SHIORI3FW.Status != 'Run' {
		username         = "ユーザーさん"
	}
	mikireflag       = 0	// 見切れ処理用

	//---- Vanishカウントを見て、真の初回か否かを判定する……必要はない（デフォだし）
	"\t\u\s[10]\h\s[5]はじめまして！\w9\w9\n\s[0]ボク、\w5%(charname(0))っていいます。\w9\n\s[6]あと、\w5となりのちっちゃいのが%(charname(1))ね。\w9\w9\n\s[5]紹介終わり！\w9\u\s[11]ちょっと待ってよそれ。\w9\w9\h\s[4]\n\nえー。\w9\w9\u\n\nちっちゃいのって言うこともないと思うんだけど。\w9\nあと、\w5誰か忘れてない？\w9\w9\h\n\n…\w5…\p2\s[214]\h\w5…\w5…\w5\s[2]あ！\w9\w9\p2まぁ、\w5いいけどさ\w5…\w5…\w9\w9\h\s[4]\n\nごめんごめん。\w9\n\s[0]この中くらいのが%(charname(2))っていいます。\w9\s[8]\nちょっと生意気なボクの弟です。\w9\w9\p2\n\n…\w5…\w5相変わらず自己紹介が下手だね。\w9\nもういいや。\w9\w9\w9\s[-1]\b[-1]\h\s[4]\n\n…\w5…\w5ええと、\w5とにかく、\w5よろしくね。\w9\w9\u\n\n\s[10]そうそう、\w5僕達は一応SSPの案内役ってことになってます。\w9\n…\w5…\w5\s[11]見てのとおりなので、\w5案内どころか足ひきずって谷底におっことしそうだけど。\w9\w9\h\n\n…\w5…\w5ごめんなさい。\w9\n\s[5]でも、\w5なんとかがんばるから、\w5わからないところがあったら呼んでね！\w9\w9\u\n\n\_a[GHOST_所長たん]SSPの開発者さんまでゴーストとして来てるみたいだし\_a、\w5そっちに聞いてみることをおすすめするよ。\w9\w9\h\s[4]\n\nそれちょっと待って。\w9\w9\w9\w9\w9\h\s[0]\n\nあ、\w5そういえば\w5…\w5…\w9\n\s[6]できれば、\w5あなたのお名前を教えてください。\w9\![open,configurationdialog,setup]\e"
}

//**** 通常の起動 ***********************************************************************

//---- OnBootイベント -------------------------------------------------------------------
OnBoot
{
	if reference[6] == "halt" {
		OnBoot_Halt
	}
	else {
		_date = STRFORM("$02d$02d",GETTIME[1],GETTIME[2])
		_script = ""
		
		//日付トーク特殊処理
		if ISVAR('lastboottalkarray') == 0 {
			lastboottalkarray = IARRAY
		}
		
		if lastbootdate_saved != _date {
			if userbirthday_date == _date {
				lastboottalkarray = (OnBoot_UserBirthday,IARRAY)
			}
			else {
				_bfunc = 'OnBoot_' + _date
				if ISFUNC(_bfunc) {
					lastboottalkarray = (EVAL(_bfunc),IARRAY)
				}
				else {
					lastboottalkarray = IARRAY
				}
			}
			lastbootdate_saved = _date
		}
		
		_n = ARRAYSIZE(lastboottalkarray)
		if _n {
			_rand = RAND(_n)
			_script = E.EvalEmbedValue(lastboottalkarray[_rand])
			lastboottalkarray[_rand] = IARRAY //消去
		}
		
		//ニュース処理
		if STRLEN(_script) == 0 {
			_script = OnBoot_NewsNeta
		}
		
		//通常起動トーク
		_timeslot = GetTimeSlot
		if STRLEN(_script) == 0 {
			_array = IARRAY
			
			if ISFUNC("OnBoot_%(_timeslot)") {
				_array ,= EVAL("OnBoot_%(_timeslot)")
			}
			if ISFUNC("BootRandomTalkSub_%(_timeslot)") {
				_array ,= EVAL("BootRandomTalkSub_%(_timeslot)")
			}
			
			if ARRAYSIZE(_array) {
				_script = E.EvalEmbedValue(_array[RAND(ARRAYSIZE(_array))])
			}
		}
		
		if STRLEN(_script) == 0 {
			_script = OnBoot_other
		}
		
		_script
	}
}

//=============== 墜落 =======================================================
OnBoot_Halt
{
	//落ちたっ！！！
	if reference[7] == "Emily" {
		reference[7] = "ボクのせい"
	}
	
	"\u\s[10]\h\s[7]落ちたっ！\w8\n%(reference[7])で落ちちゃったっ！\w9\w9\s[4]\n\n…\w5…\w5ごめんなさい。\w9\w9\u\s[10]バグ報告よろしく。\e"
	
	{
		"\u\s[10]\h\s[7]%(reference[7])で落ちたのは、\w5"
		--
		"リトルグレイの仕業だったんだよ！"
		"レジデント・オブ・サンの仕業だったんだよ！"
		"ノストラダムスによって予言されていたんだよ！"
		--
		"\w9\w9\u…\w5…\w5そんなわけないじゃない。\w9\w9\h\s[4]\n\n%(charname(1))が冷たい\w5…\w5…\w9\w9\u\n\n…\w5…\w5えーと、\w5とりあえず%(username)、\w5バグ報告よろしくね。\w9\w9\h\n\n\s[3]迷惑かけてごめんね～。\e"
	}
}

//=============== 起動 =======================================================


//--------------- ユーザー誕生日 -------------------------------------------------------

OnBoot_UserBirthday
{
	"\u\s[10]\0\s[35]あ、%(username)！\w9\w9\1やっと来たねー。\w9\w9\p[2]\s[218]ねーちゃんったら、さっきから張り切っちゃって…\w5…\w5\w9\w9\0\n\nそりゃあ、今日は%(username)の誕生日だもん。\w9\nボク、一生懸命お祝い考えたんだよ！\w9\w9\1\s[11]\n\n珍しく、美味しそうな匂いだよね。\w9\w9\0\s[56]\n\nテディの分、%(username)が食べちゃって良いみたいだよ？\w9\w9\1\s[12]\n\nわーっ、嘘ですごめんなさい！\w9\w9\p[2]\s[208]\n\n…\w5…\w5あはは。\w9\w9\0\s[55]\n\nさあ、それじゃあ最後の仕上げだよ！\w9\s[24]\n適度な演出と焦げ目は、美味しい料理の秘訣っ！\w9\w9\1\n\nわーっ！！\w9\w9\p[2]\s[202]\n\nちょ、ちょっと待ったーっ！！\0\s[-1]\n\n\1\s[-1]\n\n\p[2]\s[-1]\n\n\0\s[40]\n\n\1\s[14]\n\n\p[2]\s[240]\n\n\w9\w9\0\n\n…\w5…\w5…\w5げふっ…\w5…\w5…\w5\w9\w9\1\n\nだ、だいたい予想はしてたよね…\w5…\w5\w9\w9\p[2]\n\nばかあねきー…\w5…\w5\w9\w9\0\n\n…\w5…\w5お、おめでと、%(username)…\w5…\w5\w9\w9\1\n\nクラッカーと料理は混ぜちゃ駄目だとあれほど…\w5…\w5\e"
}

//--------------- ニュース -------------------------------------------------------
OnBoot_NewsNeta
{
	if RAND(2) != 0 { //確率1/3、それ以外は即リターン
		''
		return
	}
	
	EMGetLatestNews
}

//(てれびねた)
OnBoot_TVTalk_Before
{
	"\h\s[20]\i[%(_argv[1])]\p2\s[-1]\u\s[10]…\w5…\w5エミリもエミリオも%(_argv[0])\w5…\w5…。\w9\w9\nテレビでも見るか。\w9\n（ぽちっ）\w9\n…\w9…\w9…。\w9\n"
}

OnBoot_TVTalk_After
{
	"\w9\nリ、\w5リモコン！！\w9\n他のチャンネル！\w5他のチャンネル！！\w9\w9\h\s[4]\n\n…\w5…\w5%(_argv[0])に見入ってるところを見られてそんなに恥ずかしいの？\e"
}

//--------------- 早朝 -------------------------------------------------------
OnBoot_earlymorning : array
{
	'\u\s[10]\h\s[5]あさ～。\w9\nおはよー、\w5%(username)～。\w9\w9\u今日も一日いい日だといいね。\e'
	
	'\u\s[10]\h\s[5]おはよー、\w5はい新聞。\w9\w9\uありがと。\w9\nさて、\w5世界情勢はっと\w5…\w5…\w9\w9\h\s[4]\n\n毎朝新聞読む動物ってどうなんだろ\w5…\w5…。\w9\w9\u\s[12]\n\nお、\w5\_a[GHOST_所長たん]所長さん\_aが新聞に広告出してる！\w9\w9\h\s[2]\n\nええっ！\w9\w9\n\s[0]何て書いてあるの？\w9\w9\u\s[11]\n\nネタ募集。\w9\n年齢、\w5経験不問。\w9\n高給優遇だって。\w9\w9\h\s[4]\n\n…\w5…\w5…。\e'
}

//--------------- 朝 -------------------------------------------------------
OnBoot_morning : array
{
	{
		OnBoot_TVTalk_Before('いない',61)
		--
		'\_a[https://ja.wikipedia.org/wiki/%E3%81%95%E3%82%8F%E3%82%84%E3%81%8B3%E7%B5%84]「さわやか三組」\_aって、\w5実はかなりの長寿番組だよな。\w9\w9\h\s[5]ただいま～。\w9\u\s[12]\n\n！！！！！\w9\nオ、\w5オ、\w5オ、\w5オカエリ！！'
		--
		OnBoot_TVTalk_After('NHK教育の小学生番組')
	}
	
	'\u\s[10]\h\s[5]おはよ～！\w9\w9\h\s[31]\nお昼寝日和だね。\w5幸せ。\w9\n\w9\w9\u\s[11]\_a[GHOST_床さくら]床さくらちゃん\_aみたいだよ、\w5それじゃあ。\e'
	
	'\u\s[10]\h\s[4]むぅ\w5…\w5…\w9\w9\nこんな早朝から何か用かな\w5…\w5…？\w9\w9\u\s[11]%(charname(0))、\w5今何時だか知ってる？\e'
}

//--------------- ひるめし -------------------------------------------------------
OnBoot_lunch : array
{
	'\u\s[10]\h\s[0]お昼の時間だね。\w9\n\s[5]あ、\w5そうだ、\w5ごはん作ってあげようか、\w5%(username)。\w9\w9\u…\w5…\w5うーん、\w5たぶんやめといたほうがいいと思う。\w9\w9\h\s[4]\n\nなんでー？\e'
}

//--------------- ひるさがり -------------------------------------------------------
OnBoot_afternoon : array
{
	{
		OnBoot_TVTalk_Before('いない',61)
		--
		'ふーん、\w5にんにくを使うときは、\w5皮を剥いて１５分くらい放置してから料理するといいのか。\w9\w9\h\s[5]ただいま～。\w9\u\s[12]\n\n！！！！！\w9\nオ、\w5オ、\w5オ、\w5オカエリ！！'
		--
		OnBoot_TVTalk_After('みのもんたの番組')
	}
}

//--------------- 夜 -------------------------------------------------------
OnBoot_night : array
{
	'\u\s[10]\h\s[5]こんばんは、\w5%(username)。\w9\w9\u今日も一日おつかれさま。\e'
	
	{
		OnBoot_TVTalk_Before('いない',61)
		--
		'殺人事件って、\w5なんでいつも温泉のある観光地で起こるんだろ？\w9\w9\h\s[5]ただいま～。\w9\u\s[12]\n\n！！！！！\w9\nオ、\w5オ、\w5オ、\w5オカエリ！！'
		--
		OnBoot_TVTalk_After('二時間もののサスペンスドラマ')
	}
}

//--------------- 寝不足 -------------------------------------------------------
OnBoot_midnight : array
{
	'\u\s[10]\h\s[4]眠いね。\w9\n…\w5…\w5でも、\w5がんばる。\w9\w9\u%(username)もあまり無理しないでね。\e'
	
	'\h\s[0]\u\s[11]…\w5…\w5%(username)が夜ふかしさんだ。\w9\w9\h\s[5]いもふかしさんもいるよ～～～。\w9\w9\u\n\n夜中にいもたべるとふとるよ？\w9\w9\h\s[3]\n\nううっ、\w5我慢しよう\w5…\w5…'
	
	{
		OnBoot_TVTalk_Before('寝てしまった',65)
		--
		'SVCの形を取れる動詞は少ないのかな？\w5全部暗記した方が早いな。\w9\w9\h\s[5]テディ～。\w9\u\s[12]\n\n！！！！！\w9\nオ、\w5オ、\w5オ、\w5オハヨウ！！'
		--
		OnBoot_TVTalk_After('NHK教育の高校講座')
	}

}

//--------------- ばれんたいん -------------------------------------------------------
OnBoot_0214 : array
{
	'\u\s[10]\p2\s[206]\w9よっこらせっと。\w9\w9\h\s[2]エミリオ！\w9なにその大荷物は～！\w9\w9\p2\s[203]\n\nあ、\w5これ？\w9チョコレート。\w9\w9\s[208]\nなんだか知らないけど、\w5下駄箱やらロッカーの中からはみ出しててさ～。\w9\w9\h\s[9]\n\nうー。\w9せっかくお姉ちゃんがチョコを用意して待ってたのに！\w9\w9\u\s[11]…\w5…\w5なんで悔しがるの？\w9\w9\h\s[3]\n\nべ、\w5別に悔しがってるわけじゃ～。\e'

	'\p2\s[204]\u\s[10]\h\s[0]今日はバレンタインだけど、\w5テディは誰かからもらった？\w9\w9\u\s[11]エミリからの義理が一個だけ\w5…\w5…\w9\w9\h\s[8]\n\nあはは\w5…\w5…\w9\s[0]エミリオは？\w9\w9\p2\s[204]\_a[GHOST_舞子]舞子ねえちゃん\_aから等身大のチョコレートが\w5…\w5…\w9\w9\u\n\n私を食べて、\w5ってギャグか？\e'

	'\p2\s[200]\u\s[10]\h\s[5]ばれんたい\w2～\w2～\w2～\w2～\w2ん！\w9\p2\s[202]\u\s[12]\w9\w9\h\s[3]\n\n…\w5…\w5なんでそこで驚くのっ。\w9\w9\u\s[10]いや、\w5ついね。\w9\w9\p2\s[200]うんうん。\w9\w9\h\n\n\s[4]むうむう。\w9\n\s[0]まあいいや、\w5はいエミリオ。\w9\w9\p2\s[205]\n\nチョコプリン？\w9\w9\h\n\n\s[5]はい、\w5テディも。\w9\w9\u\s[11]\n\n…\w5…\w5ちょこ柿の種？\w9\w9\h\n\nどうだっ！\w9\w9\u\n\n…\w5…\w5…\w5…\w5…。\w9\w9\p2\n\nチョコプリンはおいしいよ、\w5うん。\w9\w9\u\n\n…\w5…\w5なにこれ。\w9\w9\h\s[4]\n\nえ～。\w5チョコ柿の種、\w5だめ？\w9\w9\u\n\n食べられないものでないだけ、\w5マシか\w5…\w5…\w9\w9\h\s[4]\n\nどういう意味～。\e'
	
	'\u\s[10]\p2\s[200]\h\s[5]ちょっ\w3ちょっ\w3ちょっ\w3ちょっ\w9ちょっこれぇと～♪\w9\w9\u\s[11]だから怪音波は\w5…\w5…\w9\w9\h\s[3]\n\nむー。\w9\w9\p2\s[205]ねぇちゃん、\w5\_a[GHOST_所長たん]所長たん\_a用のチョコドリンクは出来たの？\w9\w9\h\s[5]\n\nうん、\w5ばっちりー♪\w9\w9\u\s[10]\n\nふむ。\w9\w9\h\n\n試食してみる？\w9\nはい。\w9\w9\p2\s[206]\u\_s[1,2]\n\n（…\w5…\w5ごきゅ）\_s\w9\p2\s[202]\u\s[12]\_s[1,2]\n！！！！！\_s\w9\w9\u\n\n…\w5…\w5からい。\w9\w9\p2\n\n辛いね。\w9\h\s[2]\n\nなんでー！？\w9\w9\n\s[6]（ぐび）\w9\w9\s[2]\n！！！！！\w9\nからーい！\w9\w9\u\s[11]\n\nねえ、\w5イチゴソースとキムチベース、\w5間違えたんじゃ？\w9\w9\p2\s[204]\n\n今日はキムチ鍋の予定だったから？\w9\w9\n…\w5…\w5まさか。\w9\w9\s[-1]\n（だだだっ）\w9\w9\n（やっぱりー！！！！！）\w9\w9\u\s[11]\n\nコレがほんとのイチゴ鍋だね。\w9\w9\h\s[4]\n\nうわーん。\e'
	
	<<'
	\1\s[10]\0\s[5]ねえねえテディ、\w5エミリオ！\w9\n今年は逆チョコってのが流行ってるんだってさ！\w9\w9\p[2]\s[200]チョコレートの箱を逆にして渡すんだっけ。\w9\w9\1うんうん。\w9ツンデレ風に、\w5「あんたなんて好きじゃないんだからね！」って表現をしつつ、\w5中に豪華なチョコを入れるんだって。\w9\w9\0\s[27]\n\n…\w5…\w5そ、\w5そうだっけ？\w9\w9\p[2]\s[205]\n\n期待してるよ、\w5ねえちゃん。\w9\w9\1\n\nうんうん。\w9豪華にね。\w9\w9\0\s[4]\n\n…\w5…\w5う、\w5うん。
	============
	\1\s[10]\0\s[9]ちょっとあんたたち！\w9逆チョコって、\w5男の子が女の子にチョコを渡すことだって言うじゃない！\w9\w9\p[2]\s[202]え？\w9そうなの？\w9\w9\1知らなかったなぁ。\w9\w9\0\n\n白々しい～！\w9\w9\p[2]\s[205]\n\nじゃあ、\w5ねえちゃん。\w9\nお芋さんあげようと思ったけど、\w5チョコ買ってくるね。\w9\w9\1\n\n僕もお芋さんじゃなくてチョコにしようかな。\w9\w9\0\s[2]\n\nえ！？\w9ちょ、\w5ちょっと！\w9\w9\p[2]\s[206]\n\nお芋さんより安く上がるし、\w5良かったね。\w9テディ。\w9\w9\1\n\nうんうん。\w9余ったお金で柿の種でも買おうかな。\w9\w9\0\s[4]\n\nあーうー。\e
	'>>
	
}

//--------------- 四月馬鹿 -------------------------------------------------------
OnBoot_0401
{
	'\u\s[10]\p2\s[200]\h\s[5]テディ～、\w5テディにハガキが来てるよ。\w9\w9\uへぇ、\w5誰からだろ？\w9\w9\h\s[27]\n\nえっと、\w5「四月一日言蔵」\w5さん？？\w9\n「しがつついたち　\w5げんぞう」\w5さん？？\w9\w9\p2\s[203]え？\w9\n本当にそういう名前なの？\w9\w9\h\n\n「エイプリルフール　\w5げんぞう」\w5さん？？\w9\w9\p2\n\n「うそ　\w5いうぞう」\w5さん？？\w9\w9\u\s[17]\n\n二人とも失礼だよ！\w9\nそれは「わたぬき　\w5げんぞう」\w5って読むんだよ。\w9\w9\n\n[half]\u\s[10]昔、\w5旧暦四月一日に着物の綿を抜く行事をする習慣があってね、\w5それで\w5「四月一日」\w5で\w5「わたぬき」\w5って読むんだよ。\w9\w9\h\s[28]\p2\s[218]\_s[0,2]\n\n…\w5…\w5…。\_s\w9\w9\u\s[12]\n\nホ、\w5ホントだって！\w9\n嘘じゃなくて本当に\w5「わたぬき」\w5って読むんだって！！'
}

//--------------- ぱんつ -------------------------------------------------------
OnBoot_0802 : array
{
	'\u\s[10]\h\s[0]\p2\s[205]ねえちゃん！\w5今日はぱんつの日なんだって！！\w9\w9\h\s[3]う、\w5うそ～～～。\w9\w9\u\s[11]…\w5…\w5まぁ、\w58と2で確かにそう読めるけど\w5…\w5…\w9\w9\p2\n\nSSPナビゲーターの仕事として、\w5ちゃんと今日はしましまでないとダメなんだよね！\w9\w9\h\s[9]\n\nそんなわけないでしょっ！！'
}

//--------------- いも -------------------------------------------------------
OnBoot_1013 : array
{
	'\u\s[10]\p2\s[200]\h\s[5]ねえねえ二人とも！\w9\n今日はさつまいもの日なんだって！\w9\w9\p2\s[202]ええっ！\w5そんな日があったんだ！！\w9\w9\u埼玉の川越にさつまいも地蔵尊っていうのがあって、\w5そこで祭りが開かれる日だね。\w9\w9\h\s[27]\n\nでも、\w5ボクが言うのも何だけど、\w5なんでさつまいも地蔵なんてあるんだろうね。\w5ふしぎ。\w9\w9\p2\s[203]\n\nそういえば、\w5妙な話だよね。\w9\w9\u\n\n瀬戸内でいも類で飢えをしのいだ感謝の気持ちとして建てたいも地蔵の現代版で、\w5ガン予防に良いさつまいもを食べていつまでも健康に過ごしましょうって意味なんだってさ。\w9\w9\h\s[5]\n\nなるほど～。\w9\nボクがいつも食べてるのも実はすごくいいことなんだ！\w9\w9\u\s[11]\n\n…\w5…\w5まあ、\w5言うまでもないけど食べすぎはどんなものだろうがだめだけどね。\w9\w9\h\s[3]\n\nううっ。\w5釘さされた。'
}

//--------------- ぺけます -------------------------------------------------------
OnBoot_1224
{
	OnBoot_1225
}
OnBoot_1225 : array
{
	'\u\s[12]\p2\s[200]\h\s[5]じんぐるべー\w5　じんぐるべー\w9\nくっりっすーますー♪\w9\w9\u耳がー！\w5耳が壊れるー！\w9\w9\h\s[4]\n\nひどっ。\w9\w9\p2\s[208]あははは。\w9\w9\h\s[5]\n\nさてっ！\w5ケーキがもう直ぐ完成だよー♪\w9\w9\p2\s[203]\n\nずいぶんシンプルだね。\w9\w9\h\s[28]\n\nふっふっふ。\w9\w9\n\s[5]このおいもさんクリームでデコレーションするんだよ～♪\w9\n（ぐにょぐにょぐりぐり）\w9\w9\u\s[12]\n\n生地より多い！\w9\w9\p2\s[204]\n\nうわー。\w9\w9\h\s[5]\n\nできたっ♪\w9\nさっ、パーティーの開始だよ～♪\w9\w9\u\s[11]\n\nなんだろう、\w5このケーキというより山と表現したほうがよさそうな何かは。'
}

//--------------- 年末 -------------------------------------------------------
OnBoot_1231 : array
{
	'\h\s[0]\p2\s[200]\u\s[10]さて、\w5今年も残すところあと%(24 - GETTIME[4])時間だけど、\w5%(charname(0))は来年の目標とかある？\w9\w9\h\s[27]ん～、\w5来年は日めくりカレンダーを毎日めくるようにしたいね。\w9\w9\u\s[11]\n\n…\w5…\w5そういえば1月%(RAND(3)+5)日で止まってるね。\w9\w9\p[2]\s[204]来年も無理じゃないかな。'
}

//--------------- 年始 -------------------------------------------------------
E.DaysInYear
{
	_t = GETTIME[0]
	if (_t % 4) == 0 {
		if (_t % 100) == 0 {
			if (_t % 400) == 0 {
				366
			}
			else {
				365
			}
		}
		else {
			366
		}
	}
	else {
		365
	}
}

E.NensiNeta : array
{
	'\u\s[10]\p2\s[200]\h\s[5]さて、\w5今年も早いもので残り%(E.DaysInYear - GETTIME[2])日となりました！\w9\w9\u\s[11]\p2\s[214]\_s[1,2]まだ今年になったばかりじゃない。\_s'
	
	'\u\s[10]\p2\s[205]\h\s[5]あけました！\w9\w9\p2おめでとう。\w9\w9\uあおー。\w9\w9\h\s[8]\n\n…\w5…\w5略しすぎだって。\w9\w9\u\n\nそう？\w9\w9\h\n\nうん。\w9\w9\u\n\nまあいいや。\w9（ぼりぼり）\w9\w9\h\s[2]\n\nああっ！\w9おいもさん先にたべられた！\w9\w9\p2\s[208]\n\nいもはじめ？\w9\w9\u\s[11]\n\nむう。\w9\w9\h\s[4]\n\nわーん。'
}

OnBoot_0101
{
	E.NensiNeta
}
OnBoot_0102
{
	E.NensiNeta
}
OnBoot_0103
{
	E.NensiNeta
}

//**** 終了 *****************************************************************************

//---- OnCloseイベント ------------------------------------------------------------------
OnClose
{
	if sender != basewarename {
		"\u\s[10]\h\s[3]ごめんね～。\w9\n%(username)から言われるまで、\w5帰れないんだ。"
	}
	else {
		"\u\s[10]\h\s[5]またねー。\w9\w9\-"
	}
}

//**** シェル切り替え処理 **********************************************************

//---- シェルチェンジ開始 ---------------------------------------------------------------
OnShellChanging
{
	if reference[0] == "ちいさなエミリ" {
		//伝統のぱんつえみり特殊反応！
		"\u\s[10]\h\s[21]ちょっと恥ずかしい格好だけど、\w5着替えてくるよ。\w9\w9\uぱんつ丸見え。\w9\w9\h\s[4]\n\nそれは言わないで。\e"
	}
	elseif "[新]" _in_ reference[0] {
		""
	}
	elseif "[旧]" _in_ reference[0] {
		""
	}
	else {
		"\u\s[10]\h\s[5]おきがえー。\e"
	}
}

//---- シェルチェンジ完了 ---------------------------------------------------------------
OnShellChanged
{
	bootinit = 0
	
	if reference[0] == "ちいさなエミリ" {
		//伝統のぱんつえみり特殊反応！
		"\u\s[10]\h\s[21]あのさ、\w5まあさ、\w5とにかくさ。\w9\w9\u…\w5…\w5？\w9\w9\h\n\nそれなりの年齢になったから、\w5やっぱりこの格好は恥ずかしいんだけどね。\w9\w9\u\s[11]\n\n…\w5…\w5体だけしか成長してないくせに。\w9\w9\h\s[4]\n\nどういう意味それ～。\e"
	}
	elseif "[新]" _in_ reference[0] {
		"\u\s[10]\h\s[5]新しいボク！\w9いろいろ成長しちゃった！\w9\w9\u\s[11]でも精神的にはそのまんま。\w9\w9\h\s[9]\n\nううっ\w5…\w5…"
	}
	elseif "[旧]" _in_ reference[0] {
		"\u\s[10]\h\s[5]以前のボクに戻ったよ。"
	}
	else {
		"\u\s[10]\h\s[5]%(reference[0])におきがえ終了～。\w9\w9\u早かったね。\e"
	}
}


