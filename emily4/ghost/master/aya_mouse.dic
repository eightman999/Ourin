///////// マウス入力関連辞書

//**** 撫で反応 *************************************************************************

//---- 本体側のなで反応 -------------------------------------------------------------
Mouse_Nade0Hand : nonoverlap
{
	"\h\s[0]\u\s[11]お客さーん、\w5おさわりは禁止ですよ～？\w9\w9\h\s[8]それはなにごっこ？"
	
	"\u\s[10]\p2\s[200]\h\s[8]そんなに撫でると火が出るよ？\w9\w9\u\s[11]…\w5…\w5爆炎の間違いじゃ。\w9\w9\h\s[3]\n\n否定できないのがちょっと悲しい。\w9\w9\p2\s[208]あははは。"
	
	"\t\1\s[10]\0\s[1]…\w5…\w5すべすべしてる？\w9\w9\1そりゃもう、\w5スベスベマンジュウガニのように。\w9\w9\0\s[7]\n\nそれじゃ猛毒じゃないの！\e"
}

Mouse_Nade0Head : nonoverlap
{
	"\u\s[10]\h\s[4]むむぅ\w5…\w5…\w9\n\s[7]ボク、\w5もう子供じゃないんだからっ！\w9\w9\u\s[11]…\w5…\w5どこが？\w9\w9\h\s[4]\n\n%(charname(1))までいじめる\w5…\w5…\e"
	
	"\1\s[10]ヅラがずれるよ？\w9\nあんまりなでると。\w9\h\s[7]ヅラじゃない！\w9\w9\u\n\nじゃ、\w4付け耳？\w9\h\n\n付け耳でもない！\e"
	
	"\0\s[21]…\w4…\w4…\w4…\w4…\w9\w9\1\s[11]わざとらしく照れてもねぇ。\w9\w9\0\s[4]\n\nテディが冷たい。\e"
	
	"\u\s[10]\h\s[21]…\w5…\w5ごろごろ。\w9\w9\u\s[10]帰れ。\w9\w9\h\s[4]\n\nな、\w5なんでー。\e"
	
	"\u\s[10]\h\s[21]え、\w5えっ…\w5と\w5…\w5…。\w9\w9\nこういうのあんまり慣れてないから、\w5どう反応すればいいのか\w5…\w5…。\w9\w9\u\s[11]…\w5…。\e"
	
	"\u\s[10]\h\s[5]にゃ～。\w9\w9\u…\w5…\w5よかったね。\e"
	
	"\u\s[10]\h\s[6]…\w5…\w5にゃ。\w9\w9\uまぁ、\w5なでられるのはきもちいいよね。\w9\w9\h\s[5]\n\nにゃはは。"
}

Mouse_Nade0Face : nonoverlap
{
	"\u\s[10]\h\s[7]目からびーっ\w9\u\s[11]でないから。\e"

	"\u\s[11]\p2\s[204]\h\s[9]かわいいからってなでまくるなっ！\w9\w9\u…\w5…\w5ねえ、鏡見たほうが。\w9\w9\p2黒くなってるね。\w9\w9\h\s[2]\n\nええっ！？\e"
}

Mouse_Nade0Bust : nonoverlap
{
	if 'Phase4.5' _in_ SHIORI3FW.ShellName {
		"\t\1\s[10]\0\s[9]…\w5…\w5ちょっと大きくなったでしょっ！！\w9\w9\1…\w5…\w5自慢したいのか怒りたいのか、\w5はっきりしようよ。\e"
		"\t\1\s[10]\0\s[9]ちゃ、\w5ちゃんと育ってるんだからね！\w9\w9\1その怒り方は、\w5間違えてると思うな。\e"
		
		parallel 胸なで共通
		parallel Emily_universal
	}
	else {
		"\t\1\s[10]\0\s[2]ひゃあ！！\w9\s[9]\nちょ、\w5ちょっと！\w9\nどこに指入れてるの！\w9\w9\1…\w5…\w5入るスキマがあったんだね？\e"

		"\u\s[10]\p2\s[200]\h\s[7]下着がずれるでしょ！！\w9\w9\uつけてたの？\w9\w9\h\s[1]\n\nボクだって\w5…\w5…\w5ちゃんと成長してるもん\w5…\w5…\w9\w9\p2\s[218]さわられたくらいでずれるのは、\w5サイズでサバ読んでるからだって\_a[GHOST_舞子]舞子ねえちゃん\_aが言ってたよ。\w9\w9\h\s[4]\n\n…\w5…\w5むう\w5…\w5…\e"
		
		parallel 胸なで共通
		parallel Emily_universal
	}
}

胸なで共通 : array
{
	"\u\s[10]\h\s[1]…\w5…\w5えとさ、\w5%(charname(1))。\w9\w9\uどうしたの？\w9\w9\h\n\nもまれると大きくなるってホントかな。\w9\w9\u\n\n人生の荒波にもまれると、\w5人間的には成長するかもね。\w9\w9\h\s[4]\n\n…\w5…\w5え～と。\e"

	"\t\1\s[10]\0\s[7]女の子の大事なところを気安く触るなっ！\w9\w9\1…\w5…\w5どう大事なのか知ってる？\w9\w9\0\s[4]\n\nえ、\w5えっと…\w5…\w5\w9\n赤ちゃんが…\w5…\w5\w9\s[9]\nテディ！\w9ごはん抜き！\w9\w9\1\s[12]\n\nえ、\w5ええっ！？\e"
}

Mouse_Nade0Leg : nonoverlap
{
	"\u\s[10]\h\s[5]うひゃひゃひゃひゃっ！？\w9\w9\u\s[11]…\w5…\w5はしたないよ？\w9\w9\h\s[21]\n\nくすぐったいんだもん～。\e"

	"\t\1\s[10]\0\s[9]き、\w5気持ち悪いよっ！\w9\w9\1そうそう、\w5逆なでしたら猫は怒るからね。\w9\w9\0\n\nそういう問題じゃなくっ！\e"

	"\t\1\s[10]\0\s[9]やーめーれー！！\w9\n皮がむけるっ！！\w9\w9\1うんうん、\w5大根だから。\w9\w9\0\s[6]\n\n…\w5…\w5大根で吹き飛ばしてあげようか？\w9\w9\1\s[12]\n\nう、\w5うそですっ！\e"

	"\t\1\s[10]\0\s[31]…\w5…\w5足、\w5あんまり自信ないから…\w5…\w5\w9\w9\1へー、\w5どっか自信がある部分があるんだ。\w9\w9\0\s[21]\n\n…\w5…\w5…\w5…\w5…\w5\w9\s[4]\nない。\w9\w9\1\n\n…\w5…\w5ごめん。\e"

	"\t\1\s[10]\0\s[2]やぁんっ！\w9\w9\1…\w5…\w5今、\w5幻聴が聞こえたような…\w5…\w5\w9\w9\0\s[9]\n\nどういう意味よっ！\w9\w9\1\n\n色気もなんもないって事。\w9\w9\0\s[4]\n\nえう。\e"
}

Mouse_Nade0Ear : sequential
{
	"\h\s[4]えーと、\w5耳の中はちょっと\w5…\w5…\e"
	
	"\h\s[1]…\w5…\w5駄目だって耳の中は。\e"
	
	"\h\s[21]…\w5…\w5む～。\w9むずむずする～。\e"
	
	"\h\s[4]…\w5…\w5…\w5…\w5…。\e"
	
	"\h\s[9]がるるるるる。\w9\w9\u\s[11]そこまで怒らなくても\w5…\w5…\e"
}

Mouse_Nade0Ponytail : nonoverlap
{
	"\u\s[10]\h\s[5]さらっ　\w5さらっ　\w5へぁっ♪\w9\w9\uうむ、\w5尻尾の手入れは大事だからね。\w9\n（ふにふに）\w9\w9\h\s[3]\n\nいっしょにしないでよー。\e"
}

Mouse_Nade0Skirt : nonoverlap
{
	'\t\1\s[10]\0\s[1]…\w5…\w5布地フェチ？\w9\w9\1違うから。\e'
	"\u\s[10]\h\s[2]！！\w9\w9\n…\w5…\w5\s[1]えっち。\w9\w9\u\s[11]手の汚れ拭いてただけみたいだけど。\w9\h\s[2]えええっ！？\e"
	"\t\1\s[10]\0\s[9]やめれっ！\w9めくれるっ！！\w9\w9\1めくりたいんじゃないの？\w9\w9\0\s[1]\n\n…\w5…\w5見たいの？\w9\w9\1\n\n怖いもの見たさって、\w5すごいよね。\w9\w9\0\s[4]\n\nこわくないもん。\e"
	"\t\1\s[10]\0\s[3]…\w5…\w5何してるの？\w9\w9\1スカートの研究？\w9\w9\0\s[5]\n\nああ、\w5それなら着替えが…\w5…\w5\w9\s[27]\nあったっけ？\w9\w9\1\s[11]\n\nおい。\e"
}

Mouse_Nade0Ribbon : nonoverlap
{
	"\u\s[10]\h\s[5]（ちゃりちゃりちゃりちゃりちゃりちゃりちゃりちゃりちゃり…\w5…\w5）\w9\w9\u\s[11]やかましい。\w9\w9\h\s[4]\n\nえう。\e"
	"\t\1\s[10]\0\s[6]（ちりんちりん）\w9\w9\1風流だねえ。\w9\w9\0\s[5]\n\n鈴だけに、\w5すずしくなりまーす。\w9\w9\p[2]\s[204]…\w5…\w5寒…\w5…\w5\e"
}

Mouse_Nade0Tail
{
	Mouse_DoubleClick0Tail
}

//---- テディのなで反応 -------------------------------------------------------------
Mouse_Nade1Ear : nonoverlap
{
	"\h\s[0]\u\s[10]…\w5…\w5とぶ？　\w5とんじゃう？\w9\w9\h\s[4]…\w5…\w5本当にとべるの？\e"
	"\t\0\s[0]\1\s[10]耳はいやー。\w9\w9\0鼻はのーず。\w9\w9\p[2]\s[206]喉はすろーと。\w9\w9\1\n\n…\w5…\w5意味がよく。\e"
	"\t\0\s[0]\1\s[10]さらっ\w5さらっ\w5いやー♪\w9\w9\0\s[2]テ、\w5テディ！？\e"
	"\t\0\s[0]\1\s[10]…\w5…\w5誰、\w5ツインテールとか言うの。\w9\w9\0\s[8]その長さだもんねえ。\w9\w9\1\n\nしっぽじゃなくて耳だよ。\w9\w9\0\s[4]\n\nいや、\w5そうじゃなくて…\w5…\w5\e"
}

Mouse_Nade1Head : nonoverlap
{
	"\h\s[0]\u\s[10]…\w5…\w5悪い気分じゃないけどさ。\w9\w9\s[11]\n誰もいないときにしてくれないかな。\w9\w9\h\s[8]照れ屋だねー。\w9\w9\u\s[10]\n\n大人なの。\e"
	"\p2\s[200]\h\s[0]\u\s[10]…\w5…\w5…\w5…。　　\w9…\w5…\w5…\w5…。\w9\n…\w5…\w5…\w5…。　　\w9…\w5…\w5…\w5…。\w9\w9\n（ぴくっ）\w9\w9\n…\w5…\w5…\w5…。　　\w9…\w5…ふにゃあ。\w9\w9\h\s[5]びくとりー！\w9\w9\p2\s[204]何の勝負かなぁ。\e"
	"\h\s[0]\1\s[10]にゃあ。\w9\w9\0\s[8]…\w5…\w5え？\e"

}

//---- エミリオのなで反応 -------------------------------------------------------------
Mouse_Nade2Bust
{
	MouseDoubleClick2Bust
}

Mouse_Nade2Head : nonoverlap
{
	if ( emilio_amode ) {
		"\p2\s[206]ん\w5…\w5…\w5…\w5…\w5…。"
		
		"\p2\s[206]…\w5…\w5まぁ、\w5なでられて悪い気はしないね。"
		
		"\p2\s[206]…\w5…\w5きもちいい。"
		
		"\p2\s[206]やさしい\w5…\w5…\w5あったかい手だね。"
		
		"\p[2]\s[208]…\w5…\w5やめてよ、\w5もう…\w5…\w5。"
		
		"\p[2]\s[206]…\w5…\w5うん、\w5そこ。\w9気持ちいい。"
	}
	else {
		"\p2\s[217]…\w5…\w5子ども扱いするなよ。"
		
		"\p2\s[214]…\w5…\w5髪がぼさぼさになるって。"
		"\p[2]\s[221]…\w5…\w5何も出ないよ？"
		"\p[2]\s[217]…\w5…\w5やめてよ。"
	}
}

Mouse_Nade2Ear : nonoverlap
{
	if ( emilio_amode ) {
		"\p2\s[221]あ\w5…\w5…\w5そこは\w5…\w5…"
		
		"\p2\s[214]…\w5…\w5耳はあまり\w5…\w5…"
		
		"\p[2]\s[208]く、\w5くすぐったいなぁ…\w5…\w5"
		"\p[2]\s[214]あんっ…\w5…\w5"
	}
	else {
		"\p2\s[207]あんまり耳をぺたぺたされるの好きじゃないんだけど。"
		
		"\p2\s[217]…\w5…\w5なにやってるんだよ。"
		
		"\p[2]\s[207]さーわーるーなー！"
	}
}


//**** ホイール反応 *********************************************************************

Mouse_Wheel0Skirt : nonoverlap
{
	if TOINT(reference[2]) >= 0 {
		MouseGestureSkirt(80)
	}
}

Mouse_Wheel0Hand : nonoverlap
{
	if TOINT(reference[2]) > 0 {
		"\h\s[7]痛っ！\w5そっちは曲がらない、\w5ちゃんとっ！"
	}
	else {
		"\u\s[10]\h\s[7]ぎゅーっとにぎってぱーんち！\w9\w9\u\s[11]…\w5…\w5待て。"

		"\h\s[0]\u\s[11]（ばきぼきごきょっ！）\w9\w9\h\s[4]変な音立てないでくれるかなぁ。\w9\w9\u\s[10]\n\nむう。"
	}
}

Mouse_Wheel0Face : nonoverlap
{
	"\u\s[10]\h\s[5]ふひゃひゃひゃ！？\w9\w9\u鼻にジャストインだ。\e"

	"\p2\s[200]\h\s[0]\u\s[10]ほっぺたびろ～～ん。\w9\w9\h\s[2]やめてー！\w5顔が緩むー！\w9\w9\p2\s[205]それ以上ならないから大丈夫。\w9\w9\h\s[7]\n\nえー\w5みー\w5りー\w5おー！！\e"
	"\t\1\s[10]\0\s[9]はひふんほっ！\w9\w9\1なにすんの！\w9と言ってます。\w9\w9\0\n\nほんひゃふひへはいへ、\w5はんほはひへほ！\w9\w9\1\n\n翻訳してないで、\w5なんとかしてよ！\w9と言ってます。\w9\w9\0\n\n…\w5…\w5おやふふひ。\w9\w9\1\s[12]\n\n%(username)、\w5すぐ止めてあげてー！！\e"
}

Mouse_Wheel0Bust : nonoverlap
{
	if 'Phase4.5' _in_ SHIORI3FW.ShellName {
		"\t\1\s[10]\0\s[2]（ぷるんっ）\w9\s[9]\n…\w5…\w5…\w5…\w5揺れたよね！？\w9\w9\1…\w5…\w5だから？\e"
		"\t\1\s[10]\0\s[2]（ぷるんっ）\w9\s[4]\n…\w5…\w5本物だよ、\w5本物。\w9\w9\1本当かなぁ。\w9\w9\0\s[9]\n\n本物！\w9ちゃんと！\w9\nわかるでしょ、\w5%(username)！\w9\w9\1\s[11]\n\n同意を求めないの。\e"
		parallel 胸ホイール共通
		parallel Emily_universal
	}
	else {
		"\p2\s[200]\h\s[0]\u\s[10]…\w5…\w5ゆれない。\w9\h\s[2]！！\w9\w9\n\s[24]ゴゴゴゴゴ…\w9\u\s[12]\n\nうわー！！！\w9\w9\p2\s[204]…\w5…\w5なにしてんのさ。\e"
		"\t\1\s[10]\0\s[6]（ずるっ）\w9\s[2]\n何！\w9今の音、\w5何！！\w9\w9\1…\w5…\w5パットがずれた音じゃないの？\w9\w9\0\s[1]\n\nい、\w5入れてないもん…\w5…\w5\e"
		parallel 胸ホイール共通
		parallel Emily_universal
	}
}

胸ホイール共通 : array
{
	"\t\1\s[10]\0\s[2]ひゃんっ！\w9\w9\1あら。\w9ジャストミート？\w9\w9\0\s[9]\n\n何にだ！\e"
	"\t\1\s[10]\0\s[9]ぷ、\w5ぷるぷるしないでよ！\w9\w9\1スライムでも飼ってるの？\w9\w9\0\s[5]\n\nそうそう、\w5「ボク悪いスライムじゃないよー」\s[9]って、\w5そんなわけあるか！\w9\w9\1\n\n…\w5…\w5乗るねー。\e"
}

Mouse_Wheel0Ponytail : nonoverlap
{
	"\h\s[0]\u\s[10]よし、\w5そのまままるめて頭頂部でまとめて。\w9\w9\h\s[4]…\w5…\w5ちょんまげ禁止。\e"
	"\t\1\s[10]\0\s[4]やめてやめて。\w9結構痛いんだから。\w9\w9\1…\w5…\w5しっぽが見えちゃうから。\w9\w9\0\n\n…\w5…\w5中身なんて無いもん。\w9\w9\p[2]\s[205]知ってる。\w9\w9\0\s[9]\n\n…\w5…\w5…\w5…\w5\e"
}

Mouse_Wheel0Ribbon : nonoverlap
{
	"\u\s[10]\h\s[5]（ちゃりちゃりちゃりちゃりちゃりちゃり）\w9\n（ごとん）\w5\n\s[2]！！\w5\n（ごろごろごろごろ）\w9\w9\u\s[11]あらららら。\w9\w9\h\s[-1]\n\n（まってー！）\e"
}

Mouse_Wheel0Mouth : nonoverlap
{
	"\u\s[10]\h\s[4]びぼぼぼぼぼぼれべれれべ\w5…\w5…\w9\w9\u\s[12]おお、\w5怪電波じゃない！\w9\w9\h\n\nどぼぼぼぼべべべべべべべ\w5…\w5…\e"
	"\t\1\s[10]\0\s[4]伸びちゃう伸びちゃう。\w9\w9\1…\w5…\w5おいーっす？\e"
}

//**** つつかれ反応 *********************************************************************

//---- 本体側のつつかれ反応 -------------------------------------------------------------
Mouse_DoubleClick0
{
	OpenMenu
}

Mouse_DoubleClick0Doll
{
	if SHIORI3FW.IsVisible[1] || SHIORI3FW.IsVisible[2] {
		'\s[20]\i[66]'
	}
	else {
		OnAiTalk
	}
}

DollSignNeta : nonoverlap
{
	63
	70;71;72;73;74;75
}

Mouse_DoubleClick0DollSign
{
	if SHIORI3FW.IsVisible[1] || SHIORI3FW.IsVisible[2] {
		"\s[20]\i[%(DollSignNeta)]"
	}
	else {
		OnAiTalk
	}
}

Mouse_DoubleClick0Hand : nonoverlap
{
	"\u\s[10]\h\s[8]手？\w5つめ？\w9\n柱をがりがりひっかいたりなんかしないよ～。\w9\w9\u\s[11]…\w5…\w5ただし、\w5よく物を落っことすどじっ子の手です。\w9\w9\h\s[9]\n\nなんだと～～～。"
	
	"\u\s[10]\h\s[5]え、\w5なになに？\w9\nおいもさん持たせてくれるの？\w5やった～。\w9\w9\u\s[11]誰も何も言ってないよ。"
	
	"\u\s[10]\h\s[5]握手握手～。\w9\nにぎにぎ。\w5ぎゅ～。\w9\w9\u\s[11]…\w5…\w5良かったね。"
	
	--
	if RAND(3) == 0 { //発動確率1/3
		<<"
		
		============
		\h\s[0]\p2\s[200]\u\s[10]%(charname(0))～。\w9\w9\hん？\w9\w9\u\n\nお手！\w9\w9\h\s[5]\n\n（ぽむ）\w9\w9\n…\w5…\w5\s[2]はっ！\w9\w9\u\n\nよしよし、\w5今日も正常、\w5と。\w9\w9\p2\s[204]なにやってるのさ。\w9\w9\h\s[9]\n\nむき～っ！
		------------
		\h\s[0]\p2\s[200]\u\s[10]%(charname(0))～。\w9\w9\hん？\w9\w9\u\n\nお手！\w9\w9\h\s[28]\n\nふふーん、\w5その手には乗らないよーだ。\w9\w9\u\n\nお手とその手のギャグだね、\w5メモメモ。\w9\w9\h\s[2]\n\nえ、\w5ええええっ！？\w9\w9\p2\s[204]ちょっとそこ～、\w5うるさいよ～。
		------------
		\h\s[0]\p2\s[200]\u\s[10]%(charname(0))～。\w9\w9\hん？\w9\w9\u\n\nお手！\w9\w9\h\s[28]\n\n…\w5…\w5じ～っ。\w9\nふっ。\w5その手には乗らないよ。\w9\w9\u\n\nごほうびに芋ケンピあげようと思ってたのにな。\w9\n（ぱくっ）\w9\w9\h\s[2]\n\nな、\w5なんだって―――――！！！\w9\w9\p2\s[204]今日も平和だね。
		">>
	}
}

Mouse_DoubleClick0Head : nonoverlap
{
	"\u\s[10]\h\s[4]いたっ。\w9\w9\s[3]\n…\w5…\w5叩いたね？\w9\w9\s[7]\nこんなOS強制終了してやるっ！\w9\w9\u\s[17]洒落にならないからやめて。\e"
	
	"\h\s[4]痛い痛い。\e"
	
	"\t\1\s[10]\0\s[6]頭を一つ叩かれたら。\w9\s[24]\n思う存分やりかえしなさいって、\w5おとーさん言ってた！\w9\w9\1\s[12]言ってないー！！\e"
}

Mouse_DoubleClick0Bust : nonoverlap
{
	if SHIORI3FW.IsVisible[2] {
		"\u\s[10]\p2\s[204]\h\s[1]…\w5…\w5エミリオが見てるから、\w5ダメ。\w9\w9\u\s[11]僕は無視か……。\w9\w9\s[10]\nていうか、\w5エミリオが見てなかったらいいの？\w9\w9\h\s[6]\n\nダメ。\w9\w9\u\s[11]\n\n…\w9…\w9\w5さっきのセリフの意味は？\e"
	}
	else {
		"\u\s[10]\h\s[21]む～～\w5…\w5…。\w9\w9\nいつか仕返ししてやるんだから。\w9\w9\u\s[11]何するつもりなの\w5…\w5…。\e"
		
		"\h\s[0]\u\s[10]そのふくらみは鎧だからつついても無駄だよ。\w9\w9\h\s[7]服の下に鎧なんか装備するかっ！！\e"

		"\t\1\s[10]\0\s[7]女の子の大事なところを気安く触るなっ！\w9\w9\1…\w5…\w5どう大事なのか知ってる？\w9\w9\0\s[4]\n\nえ、\w5えっと…\w5…\w5\w9\n赤ちゃんが…\w5…\w5\w9\s[9]\nテディ！\w9ごはん抜き！\w9\w9\1\s[12]\n\nえ、\w5ええっ！？\e"

		"\t\1\s[10]\0\s[2]ひゃああ！！\w9\w9\1正しい反応を確認。\w9%(username)、\w5ご苦労様。\w9\w9\0\s[3]\n\nえ？\w9え？\w9\w9\1\n\n実はエミリがちゃんと育ってるか、\w5%(username)に確認して貰ったんだよ。\w9\w9\0\s[31]\n\nじゃあ、\w5この腹いせはテディにすればいいんだね？\w9\w9\1\n\n…\w5…\w5うそーん。\e"

		parallel Emily_universal
		
	
		{
			"\u\s[10]\h\s[1]えっと……\w9\w9そういうのは、\w5ちょっと……。\w9\w9"
			--
			"\u\s[11]…\w9…\w9。\e"
			"\u\s[10]あー。\w9余所行ってやってくれないかな。\w9\w9\h\s[4]\n\nテディ……。\e"
		}
	}
}

Mouse_DoubleClick0Ear
{
	if mouse_double_click_counter >= 3 {
		mouse_double_click_counter = 0
	}
	
	switch mouse_double_click_counter {
		"\u\s[10]\h\s[5]ぴこーん♪　\w9ぴこーん♪　\w9ぴっこーん♪\w9\w9\u\s[11]うるさいよ。\e"
		
		"\u\s[10]\h\s[5]ぴこーん♪　\w9ぴこーん♪　ぴ\w5\uもいじゃえ。\w9\w9\h\s[4]\n\n待って待って待って。\e"
		
		"\u\s[10]\h\s[5]ぴこぴこっ♪\w9\w9\p2\s[204]\w9ねえ、\w5その反応はどうかとおもうんだけど。\w9\w9\u微妙なお年頃なんだよ。\w9\nそっとしておこう。\w9\w9\h\s[4]\n\n…\w5…\w5くすん。\e"
	}
}

Mouse_DoubleClick0Ribbon : nonoverlap
{
	if emily_ribbon_flag != 1 {
		if CHAIN.IDName != "emily_ribbon" {
			<<"
			\u\s[10]\h\s[0]ん、\w5これ？\w9\n\s[21]昔からつけてる、\w5ボクの大事な鈴リボンなんだ。\w9\w9\u動き回るとちりちりうるさいって言ってたの、\w5どこの誰だっけ。\w9\w9\h\s[6]\n\nまぁ、\w5いろいろあるんだよ。\e
			============
			\h\s[0]\u\s[10]そういえば、\w5鈴リボンの話だけど、\w5かなり前からつけたままだよね。\w9\n何か理由でもあるの？\w9\w9\h\s[21]…\w5…\w5うん、\w5いろいろあるんだけど、\w5ひ\w5み\w5つ。\w9\w9\u\s[11]\n\nケチ。\w5教えてくれたっていいのに。\e
			============
			\u\s[10]\h\s[6]…\w5…\w5実は、\w5鈴リボンの話の続きなんだけどね。\w9\w9\uうん。\w9\w9\h\n\n今でこそさ、\w5こうやって一応SSPのおしごとこなしてるんだけど。\w9\w9\u\n\n…\w5…\w5どうだか。\w9\nでも、\w5前よりは慣れてきたかもね。\w9\w9\h\n\nうん。\w9\nそれで\w5…\w5…\w5最初のころに、\w5すごく緊張しちゃって、\w5ボクが失敗ばかりしてた時にね。\w9\w9\n…\w5…\w5パパから、\w5もらったんだ、\w5これ。\w9\w9\u\n\n…\w5…\w5ふ～ん。\h\x\u\c\s[10]\h\c\s[6]「何かあった時に、\w5これを静かなところで鳴らしてごらん。\w9…\w5きっと、\w5元気になれるから。」\w9\nって。\w9\w9\u…\w5…\w5いい話だね。\w9\n忘れずに、\w5心の中にしまっておこうね。\w9\w9\h\s[5]\n\nうん！\e:eval=emily_ribbon_flag=1
			">>

		}
		else {
			EMChainTalk
		}
	}
	else {
		{
			"\u\s[10]\h\s[5]ちりんちり～ん。\w9\n"
			--
			"いい音でしょ～、\w5これ。\w9\w9\uそうだね。\e"
			"いつでも鳴らしてみてね。\w9\nきっと、\w5元気になれると思うから。\w9\w9\u…\w5…\w5そうだね。\e"
		}
		"\u\s[10]\h\s[5]鈴がリンリン～♪\w9\nおしゃれでしょ～？\w9\w9\u\s[11]まさにねずみに鈴をつけられた猫の図？\w9\w9\h\s[7]\n\nなんでだ！！\e"
		
		"\u\s[10]\h\s[5]りん\w3りん\w3りりん\w3りん\w3りん\w3りりん\w3りん♪\w9\w9\u\s[11]…\w5…\w5えーと、\w5%(charname(0))って何才だっけ。\e"
		
		"\u\s[10]\h\s[27]え～と、\w5この鈴って、\w5あぶない時にはエアバッグの代わりになるんだって聞いたけど、\w5本当かな。\w9\w9\u\s[11]…\w5…\w5まさか。\e"
	}
}

Mouse_DoubleClick0Mouth : nonoverlap
{
	"\u\s[10]\h\s[9]もがもがもがー！\w9\w9\uこの口はお芋さん専用です、と申しております。\w9\h\s[7]\n\n勝手に訳すなーっ！\e"

	"\t\1\s[10]\0\s[6]（ぱく）\w9\w9\1…\w5…\w5食べ物じゃないからね。\e"

	"\t\1\s[10]\0\s[1]…\w5…\w5え、\w5えっと…\w5…\w5\w9\w9\1キスされた訳じゃないんだから。\e"
}

Mouse_DoubleClick0Ponytail : nonoverlap
{
	"\h\s[4]い、\w5痛い痛い、\w5引っ張ると痛いって。\e"
	
	"\h\s[7]これ、\w5さわると巻きついて%(username)の首をしめるようになってるんだからっ！\w9\w9\u\s[11]…\w5…\w5…。\e"

	"\t\1\s[10]\0\s[7]引っ張ったら伸びるっ！！\w9\w9\1ゴムじゃないんだから。\w9\w9\0\s[27]\n\nえ、\w5でも本当に早く伸びるみたいだよ。\w9\n毛根が刺激されて、\w5ぞわぞわっと。\w9\w9\p[2]\s[204]…\w5…\w5試さないでね、\w5%(username)。\e"
}

Mouse_DoubleClick0Tail : nonoverlap
{
	"\1\s[10]\0\s[9]さ～わ～る～な～！！\w9\w9\1それじゃ、\w5胸と同じ反応じゃない。\w9\w9\p[2]\s[218]もっと%(username)にサービスした方がいいんじゃない？\w9\w9\0\s[7]\n\nサービスってなんだー！！\e"

	"\1\s[10]\0\s[2]ふわぁぁぁぁあやああ！！\w9\w9\1そんな大げさな。\w9\w9\0\s[3]\n\nえー、\w5だっておぞかしいんだもん…\w5…\w5\w9\w9\p[2]\s[204]なにその、\w5おぞかしいって。\w9\w9\0\s[27]\n\n気持ち悪いのと、\w5くすぐったいのと、\w5いろいろ混ざった言葉？\w9\w9\1\s[11]\n\n勝手に作らないの。\w9\w9\0\s[21]\n\nそんなのわからないでしょー？\w9\n世の中広いんだし、\w5そんな言葉あるかも…\w5…\w5\w9\s[5]\nというわけで、\w5おぞかしいという言葉を知ってる方は、\w5こちらの宛先まで連絡くださーい！\w9\w9\p[2]\n\nテレビじゃないんだから…\w5…\w5\e"

	"\1\s[10]\0\s[9]おおお、\w5おんなのこのしっぽを気安く触らないでっ！！\w9\w9\1いやなら隠しておけば？\w9\w9\0\s[4]\n\n隠せるものなら隠してるー。\e"

	"\t\1\s[10]\0\s[6]（ぷすん）\w9\w9\1…\w5…\w5あ、\w5スイッチ切れた。\e"
}

//---- 相方側のつつかれ反応 -------------------------------------------------------------
Mouse_DoubleClick1 : nonoverlap
{
	if E.IsCharExist(0) {
		//エミリがいる
		"\u\s[10]メニューはエミリに言ってね。\e"
	}
	else {
		//いない
		"\u\s[10]むぅ。\w5エミリがいないから仕方ない。\w5何？\n[150]\![*]\q[%(MenuItem('なんか話して')),AITALK]\n\![X]\q[%(MenuItem('閉じる')),CANCEL_NOTALK]"
	}
}

Mouse_DoubleClick1Tail
{
	OnAiTalk
}

Mouse_DoubleClick1Wing : nonoverlap
{
	"\h\s[0]\u\s[10]触ると溶けるよ？\w9\h\s[4]え？\e"
	
	"\h\s[0]\u\s[10]実はこの羽は数千度の高温なんだ。\w9\w9\h\s[4]…\w5…\w5嘘ばっかり。\e"
	"\t\0\s[0]\1\s[10]この羽根は飴細工でできています。\w9\w9\0\s[28]食べてもいい？\w9\w9\1\n\n…\w5…\w5やめてね？\e"
}

Mouse_DoubleClick1Head : nonoverlap
{
	"\h\s[0]\u\s[12]はうぁ！！\w9\w9\h\s[4]…\w5…\w5…\w5。\e"
	"\t\0\s[0]\1\s[12]僕を踏み台にした！\w9\w9\0\s[8]違うから。\e"
}

//---- エミリオのつつかれ反応 -------------------------------------------------------------
Mouse_DoubleClick2 : nonoverlap
{
	_emilyexist = E.IsCharExist(0)
	_teddyexist = E.IsCharExist(1)
	
	if ! _emilyexist && ! _teddyexist {
		//どっちもいないので呼ぶ
		"\p2\s[204]…\w5…\w5静か過ぎる。\w9\w9\h\s[5]呼んだ？\w9\w9\u\s[10]呼んでない。\w9\w9\h\s[4]\n\n…\w5…\w5なんでテディが。\w9\w9\u\n\nふふふ。\e"
	}
	elseif ! _emilyexist {
		//エミリいない
		"\p2\s[204]いきなり呼ばれてもなぁ。\n[150]\![*]\q[%(MenuItem('なんか話して')),AITALK]\n\![X]\q[%(MenuItem('閉じる')),CANCEL_NOTALK]"
	}
	else {
		//いる
		if RAND(5) == 0 {
			"\p2\s[200]…\w5…\w5ん？\_q\n[150]\![*]\q["
			--
			if ( emilio_amode ) {
				MenuItem('甘えちゃだめ')
			}
			else {
				MenuItem('甘えてもいいよ')
			}
			--
			",EMILIO_AMODE]\n\![X]\q[%(MenuItem('なんでもない')),CANCEL_NOTALK]\_q\e"
		}
		else {
			"\p2\s[200]メニューはねえちゃんに言って。\e"
		}
	}
}

OnChoiceSelect_EMILIO_AMODE
{
	emilio_amode = ! emilio_amode
	
	if ( emilio_amode ) {
		"\p2\s[221]…\w5…\w5え\w5…\w5…\w5と、\w5それ、\w5どういう意味？\w9\w9\h\s[8]また意地張っちゃって。\w9\n素直に聞いておきなさいよ。\w9\w9\p2\n\nう～ん。"
	}
	else {
		"\p2\s[217]なんだよそれ。\w9\nさんざん子ども扱いしといて。\w9\w9\h\s[8]あ、\w5あはは\w5…\w5…"
	}
}

Mouse_DoubleClick2Face : nonoverlap
{
	if ( emilio_amode ) {
		"\p2\s[205]ん。\w5なに？"
	}
	else {
		if mouse_double_click_counter >= 5 {
			mouse_double_click_counter = 0
		}
		
		switch mouse_double_click_counter {
			"\p2\s[217]さわるなよ。"
			
			"\p2\s[207]さわるなって言ってんだろ。\e"
			
			"\p2\s[217]あのな～。\e"
			
			"\p2\s[217]…\w5…\w5…\w5…。\e"
			
			"\p2\s[204]ねぇちゃ～～～～ん。\w9\w9\h\s[8]あはは\w5…\w5…\w9\nはいはい、\w5泣かない泣かない。\e"
		}
	}
}

Mouse_DoubleClick2Bust : nonoverlap
{
	if ( emilio_amode ) {
		"\p2\s[214]むぅ\w5…\w5…\w5そんなところ触るのはちょっと\w5…\w5…"
		
		"\p2\s[203]…\w5…\w5ごめん、\w5やめてくれないかな。"
		
		"\p[2]\s[221]…\w5…\w5ううん、\w5別に…\w5…\w5我慢できるけど…\w5…\w5"
		"\p[2]\s[203]…\w5…\w5あまり、\w5気持ちよくない…\w5…\w5かな。"
	}
	else {
		"\p2\s[217]さわりたいならねえちゃんのさわれよ。\w9\w9\h\s[7]エ・\w5ミ・\w5リ・\w5オ～～～！！\e"
		
		"\p2\s[207]僕は女の子じゃないっ！\e"
		
		"\p2\s[206]無いよ。\w5ねえちゃんもだけど。\w9\w9\h\s[4]…\w5…\w5…。\e"
		
		"\p2\s[217]なんだか手つきがやらしいなあ。\e"
		
		"\p2\s[205]せっかくだからねぇちゃんのさわってあげなよ。\w9\h\s[9]なんでっ！\e"
	}
}

Emily_universal : array
{
	"\t\0\s[2]\1\s[10]１回５００円いただきます。\w9\w9\0\s[7]ボクはそんなにお安くないぞ！\w9\s[4]\nというか、\w5ボクの体で商売しないで…\w5…\w5\e"

	"\t\1\s[10]\0\s[3]…\w5…\w5そういう事すると…\w5…\w5\w9\nボク、\w5困るよ。\w9\w9\1エミリも女の子らしくなったねえ。\w9\w9\p[2]\s[203]…\w5…\w5冷静に判断するべきところなのかな。\w9\w9\0\s[31]\n\n…\w5…\w5そりゃあ…\w5…\w5うん、\w5ボクだって女の子だよ。\w9\w9\1\n\n…\w5…\w5どうしよう、\w5変なスイッチ入っちゃった？\w9\w9\p[2]\s[208]\n\nほっとこうか。\e"

	"\t\1\s[10]\0\s[28]…\w5…\w5敵性反応捕捉。\w9\nこれより\s[24]反撃しますっ！！\w9\w9\1\s[12]やめ――――――！！\e"

	"\t\1\s[10]\0\s[9]…\w5…\w5ごめんなさい、\w5こんな時にどんな顔したらいいのか判らないの。\w9\w9\1…\w5…\w5判りやすい嘘で。\e"

	"\t\1\s[10]\0\s[6]%(username)。\w9\s[3]\n責任、\w5取ってくれるんだよね？\w9\w9\1…\w5…\w5いきなりそんな。\w9\w9\0\s[24]\n\n責任、\w5取ってくれるよね…\w5…\w5？\w9\w9\1\s[12]\n\nちょっと待て―――――――！\e"
}

//**** クリック *********************************************************************
Mouse_Click4newbies_article1
{
	'\p[2]\s[200]\u\s[10]\h\s[5]まずは、\w5ボクたちに話しかける方法を覚えようね。\w9\w9\uエミリにマウスカーソルを当てると、\w5指の形や、\w5手のひらの形になる場所があるよね。\w9\w9\p[2]\s[205]そこでクリックしちゃうと、\w5ねえちゃんが喜んじゃうから、\w5まずはやらないでね。\w9\h\s[9]\n\n喜ばないっ！！\w9\s[21]\n撫でて貰ったら、\w5嬉しいけど\w5…\w5…\w9\w9\u\n\nそれはまた今度ね。\w9\w9\h\s[5]\n\nというわけで、\w5とりあえず矢印カーソルのところでボクをダブルクリックしてみてね。\w9\n吹き出しの中に、\w5いろいろなメニューがでてくるよ。\w9\w9\p[2]\n\nどれを選んでも、\w5パソコンが壊れたりとかは絶対ないから、\w5いろいろ試してみてね。\e'
}

Mouse_Click4newbies_article2
{
	'\p[2]\s[200]\u\s[10]\h\s[5]ボクたちに話しかける方法は、\w5判って貰えた？\w9\w9\u用事がないときは、\w5僕達は勝手にしゃべってるからね。\w9\w9\h\n\n今度は、\w5ボクを右クリックしてみてね。\w9\w9\u\n\n右クリックは、\w5エミリの上ならどこでも大丈夫だよ。\w9\nあと、\w5タスクトレイのアイコンでも同じことができるね。\w9\w9\p[2]\s[205]細長い、\w5四角いメニューが出るよね。\w9\nそれが、\w5SSP本体のメニューなんだ。\w9\n僕たち以外のキャラクターに交代するときは、\w5「ゴースト切り替え」、\w5僕たちと一緒に呼びたいときは、\w5「ゴースト呼び出し」から、\w5他のキャラクターを呼んでね。\w9\w9\u\n\nSSPを終了するときも、\w5この右クリックメニューから「終了」を選ぶんだ。\w9\w9\h\n\nいろいろ試してみてね～。\w9\s[4]\nでも、\w5「アンインストール」は、\w5できればしないでほしいな\w5…\w5…\w9\w9\p[2]\s[203]\n\n僕たちが消えちゃうからね。\w9\w9\u\s[11]\n\n寂しいけど、\w5僕たちが飽きられちゃったら、\w5いつかは\w5…\w5…\w9\w9\h\n\n…\w5…\w5…\w9\w9\p[2]\n\n…\w5…\w5…\w9\w9\h\s[7]\n\nああもう、\w5しけった話は今は考えないの！\w9\nまだ始まったばかりなんだし！\w9\w9\u\n\nま、\w5まぁ\w5…\w5…\w9\s[10]\nとにかく、\w5本体の操作をしたいときには右クリックって覚えてね。\e'
}

Mouse_Click4newbies_article3
{
	'\p[2]\s[200]\u\s[10]\h\s[5]ボクたちのお勉強、\w5ネットワーク更新についてだよ。\w9\w9\uこれを押すと、\w5僕たちの新しいおしゃべりや、\w5新しい機能が追加される\w5…\w5…\w5こともあるんだ。\w9\w9\p[2]\s[205]常に更新されるとは限らないけど、\w5思い出した時にでも、\w5押してみてね。\w9\w9\h\s[27]\n\n何かおかしいなー、\w9\s[5]\nと思ったときも、\w5これをやってみると直る場合があるからね。\w9\w9\p[2]\n\nもし直らなかったら、\w5キャラクターをダウンロードした所に報告してあげてね。\w9\w9\u\n\nバグ報告は、\w5開発者さんの励みにもなるし、\w5感想も送ってあげると喜ぶと思うよ。\w9\w9\h\s[0]\n\nあ、\w5あとね。\w9\nキャラクターを追加した時は、\w5必ずネットワーク更新をしてみてね。\w9\w9\u\n\n最新版が、\w5かならずnarファイルになってるわけじゃないからね。\e'
}

Mouse_Click4newbies_article4
{
	'\p[2]\s[200]\u\s[10]\h\s[5]ボクたち以外のキャラクターの探し方だよ。\w9\w9\uSSPで活動できるキャラクターは、\w51000人を越えてるんだ。\w9\n「ゴースト」っていうんだけどね。\w9\w9\p[2]\s[200]僕たちも、\w5SSPのデフォルトキャラクターであり、\w5その中の一組でもあるんだよ。\w9\w9\h\s[3]\n\nそんないっぱいの中から探せないよ\w5…\w5…\w9\s[5]\nって思った？\w9\w9\u\n\n大丈夫、\w5「ゴーストキャプターさくら」や、\w5「ghost-log」、\w5「GHOST TOWN」という所から、\w5簡単に探せるよ。\w9\w9\p[2]\s[205]\n\n%usernameのお気に入りなキャラクターも、\w5きっといると思うから探してみてね。\w9\w9\u\n\nお迎えの仕方は簡単。\w9\nまず、\w5「ゴーストキャプターさくら」なら、\w5一番上にある「登録順に切り替え」を押して、\w5お気に入りのキャラクターを探してね。\w9\w9\h\n\nこれだ！\w9と思う人がいたら、\w5リンクを押してみてね。\w9\w9\u\n\n残念ながら、\w5今はもうお迎えできない人達もいるけど、\w5だいたいは、\w5実家のページに行けるから、\w5その先で「Narアーカイブ」を探してね。\w9\nそれを保存して、\w5Narファイルを僕たちにドラッグ＆ドロップするとあらフシギ。\w9\w9\p[2]\n\n新しい人達のお出まし、\w5って訳。\w9\w9\h\n\n楽しい人達ばっかりだから、\w5是非いろんな人達を呼んでみてね。\h\_q\n\n[half]\_a[http://buynowforsale.shillest.net/ghosts/]ゴーストキャプターさくら\_a\n\_a[https://ghost-log.net/]ghost-log\_a\n\_a[http://ghosttown.mikage.jp/]GHOST TOWN\_a\e'
}

Mouse_Click4newbies_article5
{
	'\u\s[10]\h\s[5]じゃ、\w5説明書を開いてみるね。\w9\w9\u僕達の知ってること以外にも、\w5色々書いてあるね。\w9\n\s[11]まあ、\w5あんまり読まなくても使えると思うけど。\w9\w9\h\s[8]\n\nあ、\w5あはは\w5…\w5…\w5\![open,help]'
}

Mouse_Click4newbies_close
{
	'\p[2]\s[200]\u\s[10]\h\s[3]む～、\w5もう閉じちゃっていいの？\w9\w9\u\s[11]もういいって言ってるんだからいいんでしょ。\w9\w9\p2\s[205]また見たくなったら、\w5ねえちゃんをダブルクリックして、\w5設定を変えて、\w5から戻せるよ。\h'
	--
	'\_q\n\n\![*]\q[' + MenuItem('閉じる') + ',CLOSE_NEWBIES]'
	--
	'\n\![X]\q[' + MenuItem('キャンセル') + ',CANCEL_NOTALK]'
}

OnChoiceSelect_CLOSE_NEWBIES
{
	config_newbies_info_disable = 1
	ShowHideNewbiesInfo(0)
	--
	'\u\s[10]\h\s[5]閉じたよ～。\w9\w9\uまた見たい時は、\w5設定を変えて、\w5からね。'
}


//**** ジェスチャ *********************************************************************

MouseGestureSkirt
{
	if 'Phase4.5' _in_ SHIORI3FW.ShellName {
		_s = "\s[%(_argv[0])]"

		"\1\s[10]\0%(_s)ひゃあ！？\w9\w9\1大変よくめくれました。\w9\w9\0\s[7]\n\nお黙りっ！\e"

		"\1\s[10]\0%(_s)ちょ、\w5ちょっとぉ！\w9\w9\1風のいたずらだね。\w9\w9\0\s[4]\n\n明らかに%(username)がめくったよ…\w5…\w5\e"

		"\1\s[10]\0%(_s)きゃっ！\w9\w9\1\s[19]ダメでしょエミリ、\w5ちゃんとしましまぱんつ穿かないと！\w9\n制服なんだから！\w9\w9\0\s[24]\n\nいつからそんな制服になった！\e"

		"\1\s[10]\0%(_s)ああん！\w9\w9\p[2]\s[209]…\w5…\w5…\w5\w9\w9\1\s[11]…\w5…\w5…\w5\w9\w9\0\s[9]\n\n二人とも、\w5なんで反応しないの！？\w9\w9\p[2]\n\n見飽きた。\w9\w9\1\n\n見慣れた。\w9\w9\0\n\n…\w5…\w5…\w5あんたらは…\w5…\w5\e"

		"\1\s[10]\0%(_s)あっ…\w5…\w5\w9\w9\1…\w5…\w5耳がおかしくなったのかな。\w9\nエミリが女の子みたいな悲鳴を上げた気がする。\w9\w9\0\s[9]\n\n今確かに上げましたってば！\e"
	}
	else {
		"\u\s[10]\h\s[9]め～\w5く～\w5る～\w5な～！！\w9\w9\u\s[11]お年頃か\w5…\w5…\w9\w9\h\s[21]\n\n今日は\w5…\w5はいてな\w5…\w5…\w9\w9\u\s[12]\n\n！？\w9\w9\h\s[1]\n\n間違って全部洗濯しちゃった\w5…\w5…\w9\w9\u\s[11]\n\n…\w5…。\w9\w9\h\s[8]\n\nや、\w5やだなぁ、\w5冗談だってば。\w9\w9\u\n\n…\w5…\w5%(charname(0))が言うと冗談に聞こえないから。\w9\w9\h\s[4]\n\nぇぅー。\e"
		
		"\u\s[11]\p2\s[218]\h\s[2]にゃあああああ！？\w9\w9\p2物好きがいるね。\w9\w9\uそうだね。\w9\w9\h\s[9]\n\nなんでっ！？\w9\n乙女のスカートの中は秘密でいっぱいなんだよっ！？\w9\w9\u\s[10]\n\n…\w5…\w5ふう。\w9\w9\p2\n\n…\w5…\w5恥ずかしい台詞だね。\w9\w9\h\s[7]\n\nなんでだー！？\e"

		parallel Emily_universal
	
	}
}

OnMouseGestureCall0Skirt_left
{
	MouseGestureSkirt(80)
}

OnMouseGestureCall0Skirt_right
{
	MouseGestureSkirt(81)
}

OnMouseGestureCall0Skirt_left_up
{
	MouseGestureSkirt(80)
}

OnMouseGestureCall0Skirt_right_up
{
	MouseGestureSkirt(81)
}

OnMouseGestureCall0Skirt_up
{
	MouseGestureSkirt(81)
}

